// Copyright (C) 2020  The Mirage Authors
//
// This program is free software; you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation; either version 2 of the License, or
// (at your option) any later version.
//
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

.section #boot_stage_code

_start:
    // Extract the size of the Falcon DMEM segment from MMIO
    // and initialize the stack pointer to the end of it.
    mov $r13 #FALCON_HWCFG
    iord $r13 I[$r13]
    shr b32 $r13 0x9
    and $r13 0x1FF
    shl b32 $r13 0x8
    mov $sp $r13

    lcall #main

    exit

// Locals
pushdef(`key_data_buffer', `$r10')
pushdef(`key_data_address', `$r11')
pushdef(`key_data_size', `$r12')
pushdef(`key_data_start', `$r0')
pushdef(`blob0_size', `$r9')
pushdef(`blob1_size', `$r15')
pushdef(`blob2_size', `$r14')
pushdef(`blob4_size', `$r13')
pushdef(`blob3_address', `$r9')

.align 4
main:
    // Prepare some important registers.
    mov $r9 -0x10
    mov key_data_address #key_data_table_start
    mov key_data_size #key_data_table_end

    // Set up the stack.
    add $sp -0x94
    mpush key_data_start

    // Copy the Key Data blob from IMEM onto the stack.
    mov key_data_start $sp
    add b32 key_data_start key_data_start 0x14
    and key_data_start $r9
    mov b32 key_data_buffer key_data_start
    lcall #memcpy_i2d

    // Extract the sizes of the individual stages from the Key Data buffer.
    ld b32 blob4_size D[key_data_start + #SECUREBOOT_SIZE]
    ld b32 blob0_size D[key_data_start + #BOOT_SIZE]
    ld b32 blob1_size D[key_data_start + #KEYGENLDR_SIZE]
    ld b32 blob2_size D[key_data_start + #KEYGEN_SIZE]

    // Sum up the stage sizes to get the start address of the next stage.
    add b32 blob3_address blob0_size blob1_size
    add b32 blob3_address #key_data_table_end_aligned
    add b32 blob3_address blob3_address blob2_size
    add b32 blob3_address blob3_address blob4_size

    // Jump to the start of the SecureBootLdr blob.
    call blob3_address

    // Clean up and return to init code.
    clear b32 key_data_buffer
    mpopaddret key_data_start 0x94

// End locals
popdef(`blob3_address')
popdef(`blob4_size')
popdef(`blob2_size')
popdef(`blob1_size')
popdef(`blob0_size')
popdef(`key_data_start')
popdef(`key_data_size')
popdef(`key_data_address')
popdef(`key_data_buffer')

// Standard library includes for #memcpy_i2d.
include(`mmio.fuc')
include(`memcpy_i2d.fuc')

.align 0x100
.section #boot_stage_data

// Reserve enough space for the Key Data blob before the code.
// This only acts as a placeholder to reserve the storage and will be overridden
// during the build process.
.align 0x10
key_data_table_start:
DEBUG_KEY:          .skip 0x10
BOOT_MAC:           .skip 0x10
KEYGENLDR_MAC:      .skip 0x10
KEYGEN_MAC:         .skip 0x10
KEYGEN_IV:          .skip 0x10
HOVI_EKS_SEED:      .skip 0x10
HOVI_COMMON_SEED:   .skip 0x10
BOOT_SIZE:          .b32 0x00000000
KEYGENLDR_SIZE:     .b32 0x00000000
KEYGEN_SIZE:        .b32 0x00000000
SECUREBOOTLDR_SIZE: .b32 0x00000000
SECUREBOOT_SIZE:    .b32 0x00000000
key_data_table_end:
.align 0x100
key_data_table_end_aligned:
